MODULE YOMGFL

USE PARKIND1, ONLY : JPRB, JPIM
USE YOM_YGFL, ONLY : TYPE_GFLD !! TYPE_GFL_DESC

IMPLICIT NONE
SAVE

!     -------------------------------------------------------------------------

TYPE :: TGFL

!*    GFL gridpoint arrays 
!     For the part of GFL that maps into spectral space see yomsp.F90 (SPGFL etc.)
!     All arrays have the layout(NPROMA,NFLEVG,"number of variables",NGPBLKS)

! GFL        - main GFL array holding t0 and t1 GFL variables
! GFLT1      - GFL array for t+dt quantities
! GFLSLP     - GFL array for use in semi-lagrangian physics
! GFLPT      - physics tendency GFL array
! GFLPC      - predictor/corrector auxiliary arrays (3TL)

! GFL5       - trajectory GFL array
! GFLSLP5    - trajectory for GFLSLP
! GFL_DEPART - used in 3-D FGAT (LIDMODEL)

! YGFL       - pointer to the YGFL structure stored in the model object : new in CY45
!     -------------------------------------------------------------------------

REAL(KIND=JPRB), ALLOCATABLE :: GFL   (:,:,:,:)
REAL(KIND=JPRB), ALLOCATABLE :: GFLT1 (:,:,:,:)
REAL(KIND=JPRB), ALLOCATABLE :: GFLSLP(:,:,:,:)
REAL(KIND=JPRB), ALLOCATABLE :: GFLPT (:,:,:,:)
REAL(KIND=JPRB), ALLOCATABLE :: GFLPC (:,:,:,:)

REAL(KIND=JPRB), ALLOCATABLE :: GFL5       (:,:,:,:)
REAL(KIND=JPRB), ALLOCATABLE :: GFLSLP5    (:,:,:,:)
REAL(KIND=JPRB), ALLOCATABLE :: GFL_DEPART (:,:,:,:)

TYPE(TYPE_GFLD), POINTER :: YGFL => NULL()

END TYPE TGFL

REAL(KIND=JPRB),ALLOCATABLE :: GFL_LHN  (:)
REAL(KIND=JPRB),ALLOCATABLE :: GFL_WKA  (:,:)
REAL(KIND=JPRB),ALLOCATABLE :: GFL_WKA2  (:,:,:)

!     -------------------------------------------------------------------------

CONTAINS

SUBROUTINE ZERO_YOMGFL(SELF)

USE YOMHOOK,  ONLY : LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

TYPE(TGFL), INTENT(INOUT) :: SELF

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('YOMGFL:ZERO_YOMGFL',0,ZHOOK_HANDLE)

IF (ALLOCATED(SELF%GFL))    SELF%GFL    = 0.0_JPRB
IF (ALLOCATED(SELF%GFLT1))  SELF%GFLT1  = 0.0_JPRB
IF (ALLOCATED(SELF%GFLSLP)) SELF%GFLSLP = 0.0_JPRB
IF (ALLOCATED(SELF%GFLPT))  SELF%GFLPT  = 0.0_JPRB
IF (ALLOCATED(SELF%GFLPC))  SELF%GFLPC  = 0.0_JPRB

IF (LHOOK) CALL DR_HOOK('YOMGFL:ZERO_YOMGFL',1,ZHOOK_HANDLE)

END SUBROUTINE ZERO_YOMGFL

!-------------------------------------------------------------------------

SUBROUTINE COPY_YOMGFL(SELF,RHS)

USE YOMHOOK , ONLY : LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

TYPE(TGFL), INTENT(INOUT) :: SELF
TYPE(TGFL), INTENT(IN)    :: RHS

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

#include "abor1.intfb.h"

IF (LHOOK) CALL DR_HOOK('YOMGFL:COPY_YOMGFL',0,ZHOOK_HANDLE)

IF (ALLOCATED(RHS%GFLT1) .AND. .NOT.ALLOCATED(SELF%GFLT1)) THEN
  ALLOCATE(SELF%GFLT1(LBOUND(RHS%GFLT1,1):UBOUND(RHS%GFLT1,1), LBOUND(RHS%GFLT1,2):UBOUND(RHS%GFLT1,2),&
 &                    LBOUND(RHS%GFLT1,3):UBOUND(RHS%GFLT1,3), LBOUND(RHS%GFLT1,4):UBOUND(RHS%GFLT1,4) ))
ENDIF


IF (ALLOCATED(SELF%GFL) .AND. ALLOCATED(RHS%GFL)) THEN
  IF (ALL(SHAPE(SELF%GFL) == SHAPE(RHS%GFL))) THEN
    SELF%GFL = RHS%GFL
  ELSE
    CALL ABOR1("YOMGFL:COPY GFL different shapes")
  ENDIF
ELSE
  CALL ABOR1("YOMGFL:COPY trying to copy into unallocated GFL array")
ENDIF

IF (ALLOCATED(SELF%GFLT1) .AND. ALLOCATED(RHS%GFLT1)) THEN
  IF (ALL(SHAPE(SELF%GFLT1) == SHAPE(RHS%GFLT1))) THEN
    SELF%GFLT1 = RHS%GFLT1
  ELSE
    CALL ABOR1("YOMGFL:COPY GFLT1 different shapes")
  ENDIF
ENDIF

IF (ALLOCATED(SELF%GFLSLP) .AND. ALLOCATED(RHS%GFLSLP)) THEN
  IF (ALL(SHAPE(SELF%GFLSLP) == SHAPE(RHS%GFLSLP))) THEN
    SELF%GFLSLP    = RHS%GFLSLP
  ELSE
    CALL ABOR1("YOMGFL:COPY GFLSLP different shapes")
  ENDIF
ENDIF

IF (ALLOCATED(SELF%GFLPT) .AND. ALLOCATED(RHS%GFLPT)) THEN
  IF (ALL(SHAPE(SELF%GFLPT) == SHAPE(RHS%GFLPT))) THEN
    SELF%GFLPT = RHS%GFLPT
  ELSE
    CALL ABOR1("YOMGFL:COPY GFLPT different shapes")
  ENDIF
ENDIF

IF (ALLOCATED(SELF%GFLPC) .AND. ALLOCATED(RHS%GFLPC)) THEN
  IF (ALL(SHAPE(SELF%GFLPC) == SHAPE(RHS%GFLPC))) THEN
    SELF%GFLPC = RHS%GFLPC
  ELSE
    CALL ABOR1("YOMGFL:COPY GFLPC different shapes")
  ENDIF
ENDIF

IF (LHOOK) CALL DR_HOOK('YOMGFL:COPY_YOMGFL',1,ZHOOK_HANDLE)

END SUBROUTINE COPY_YOMGFL

!-------------------------------------------------------------------------

SUBROUTINE MUL_YOMGFL(SELF,PZ)
 
USE YOMHOOK,  ONLY : LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

TYPE(TGFL),      INTENT(INOUT) :: SELF
REAL(KIND=JPRB), INTENT(IN)    :: PZ

INTEGER(KIND=JPIM) :: IBL
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('YOMGFL:MUL_YOMGFL',0,ZHOOK_HANDLE)
IF (ALLOCATED(SELF%GFL))  THEN
!$OMP PARALLEL DO PRIVATE(IBL)
  DO IBL = 1, SIZE(SELF%GFL,DIM=4)
    SELF%GFL(:,:,:,IBL) = PZ*SELF%GFL(:,:,:,IBL)
  ENDDO
!$OMP END PARALLEL DO
ELSE
  CALL ABOR1("YOMGFL:MUL_YOMGFL argument not allocated")
ENDIF

IF (LHOOK) CALL DR_HOOK('YOMGFL:MUL_YOMGFL',1,ZHOOK_HANDLE)

END SUBROUTINE MUL_YOMGFL

!-------------------------------------------------------------------------

SUBROUTINE AXPBY_YOMGFL(SELF,PA,RHS,PB)
 
USE YOMHOOK,  ONLY : LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

TYPE(TGFL),      INTENT(INOUT) :: SELF
REAL(KIND=JPRB), INTENT(IN)    :: PA
TYPE(TGFL),      INTENT(IN)    :: RHS
REAL(KIND=JPRB), INTENT(IN)    :: PB

INTEGER(KIND=JPIM) :: IBL
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

#include "abor1.intfb.h"

IF (LHOOK) CALL DR_HOOK('YOMGFL:AXPBY_YOMGFL',0,ZHOOK_HANDLE)
IF (ALLOCATED(SELF%GFL) .AND. ALLOCATED(RHS%GFL)) THEN
  IF (ALL(SHAPE(SELF%GFL) == SHAPE(RHS%GFL))) THEN
!$OMP PARALLEL DO PRIVATE(IBL)
    DO IBL = 1, SIZE(SELF%GFL,DIM=4)
      SELF%GFL(:,:,:,IBL) = PA*SELF%GFL(:,:,:,IBL) + PB*RHS%GFL(:,:,:,IBL)
    ENDDO
!$OMP END PARALLEL DO
  ELSE
    CALL ABOR1("YOMGFL:AXPBY_YOMGFL different shapes")
  ENDIF
ELSE
  CALL ABOR1("YOMGFL:AXPBY_YOMGFL argument not allocated")
ENDIF

IF (LHOOK) CALL DR_HOOK('YOMGFL:AXPBY_YOMGFL',1,ZHOOK_HANDLE)

END SUBROUTINE AXPBY_YOMGFL


!-------------------------------------------------------------------------

SUBROUTINE DIFF_YOMGFL(SELF,RHS)

USE YOMHOOK,  ONLY : LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

TYPE(TGFL),      INTENT(INOUT) :: SELF
TYPE(TGFL),      INTENT(IN)    :: RHS

INTEGER(KIND=JPIM) :: IBL
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

#include "abor1.intfb.h"

IF (LHOOK) CALL DR_HOOK('YOMGFL:DIFF_YOMGFL',0,ZHOOK_HANDLE)
IF (ALLOCATED(SELF%GFL) .AND. ALLOCATED(RHS%GFL)) THEN
  IF (ALL(SHAPE(SELF%GFL) == SHAPE(RHS%GFL))) THEN
!$OMP PARALLEL DO PRIVATE(IBL)
    DO IBL = 1, SIZE(SELF%GFL,DIM=4)
      SELF%GFL(:,:,:,IBL) = SELF%GFL(:,:,:,IBL) - RHS%GFL(:,:,:,IBL)
    ENDDO
!$OMP END PARALLEL DO
  ELSE
    CALL ABOR1("YOMGFL:DIFF_YOMGFL different shapes")
  ENDIF
ELSE
  CALL ABOR1("YOMGFL:DIFF_YOMGFL argument not allocated")
ENDIF

IF (LHOOK) CALL DR_HOOK('YOMGFL:DIFF_YOMGFL',1,ZHOOK_HANDLE)

END SUBROUTINE DIFF_YOMGFL


!-------------------------------------------------------------------------

SUBROUTINE DOT_PROD_YOMGFL(YDGEOMETRY,FLD1,FLD2,PPROD)

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOMHOOK,  ONLY : LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

TYPE(GEOMETRY),  INTENT(IN)  :: YDGEOMETRY
TYPE(TGFL),      INTENT(IN)  :: FLD1
TYPE(TGFL),      INTENT(IN)  :: FLD2
REAL(KIND=JPRB), INTENT(OUT) :: PPROD

INTEGER(KIND=JPIM) :: JF
REAL(KIND=JPRB)    :: ZTMP
REAL(KIND=JPHOOK)    :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('YOMGFL:DOT_PROD_YOMGFL',0,ZHOOK_HANDLE)

IF (LHOOK) CALL DR_HOOK('YOMGFL:DOT_PROD_YOMGFL',1,ZHOOK_HANDLE)

END SUBROUTINE DOT_PROD_YOMGFL

!-------------------------------------------------------------------------

SUBROUTINE RANDOM_YOMGFL(SELF)
 
USE YOMHOOK,  ONLY : LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

TYPE(TGFL), INTENT(INOUT) :: SELF

INTEGER(KIND=JPIM) :: IBL, JF
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('YOMGFL:RANDOM_YOMGFL',0,ZHOOK_HANDLE)
! Just a plain RANDOM_NUMBER for now. 

IF (ALLOCATED(SELF%GFL))  THEN
!$OMP PARALLEL DO PRIVATE(IBL)
  DO IBL = 1, SIZE(SELF%GFL,DIM=4)
    DO JF=1,SIZE(SELF%GFL,DIM=3)
      CALL RANDOM_NUMBER(SELF%GFL(:,:,JF,IBL))
      SELF%GFL(:,:,JF,IBL) = 2.0_JPRB*SELF%GFL(:,:,JF,IBL) + 1.0_JPRB
    ENDDO
  ENDDO
!$OMP END PARALLEL DO  
ELSE
  CALL ABOR1("YOMGFL:RANDOM_YOMGFL argument not allocated")
ENDIF

IF (LHOOK) CALL DR_HOOK('YOMGFL:RANDOM_YOMGFL',1,ZHOOK_HANDLE)

END SUBROUTINE RANDOM_YOMGFL

END MODULE YOMGFL
