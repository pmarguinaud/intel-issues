SUBROUTINE APL_ARPEGE(YDMF_PHYS_BASE_STATE, YDMF_PHYS_NEXT_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, &
& YDCPG_MISC, YDCPG_GPAR, YDCPG_PHY0, YDMF_PHYS, YDCPG_DYN0, YDMF_PHYS_SURF, YDCPG_SL2, YDVARS, YDCFU,&
& YDMODEL, YDDDH, YDPHYSMWAVE)

!**** *APL_ARPEGE*  - Call ARPEGE physics

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

!     Modifications.
!     --------------
!     13-Jul-2022 R. El Khatib Fix initialization of ZQGM
!     R. El Khatib 26-Apr-2023 Loop pushing in ppwetpoint
!     04-Oct-2023 A. Alias initialization of ZFCHQ added
! End Modifications
!-------------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_GPAR_TYPE, CPG_PHY_TYPE
USE CPG_SL2_TYPE_MOD   , ONLY : CPG_SL2_TYPE
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD  &
                     & , ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE MF_PHYS_BASE_STATE_TYPE_MOD &
                     & , ONLY : MF_PHYS_BASE_STATE_TYPE
USE MF_PHYS_NEXT_STATE_TYPE_MOD &
                     & , ONLY : MF_PHYS_NEXT_STATE_TYPE
USE TYPE_MODEL         , ONLY : MODEL

USE PARKIND1           , ONLY : JPIM     ,JPRB
USE YOMHOOK            , ONLY : LHOOK    ,DR_HOOK, JPHOOK
USE DDH_MIX            , ONLY : TYP_DDH

USE YOE_PHYS_MWAVE     , ONLY : TEPHYSMWAVE
USE YOMCFU             , ONLY : TCFU 

IMPLICIT NONE

TYPE(MF_PHYS_BASE_STATE_TYPE),  INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(MF_PHYS_NEXT_STATE_TYPE),  INTENT(INOUT) :: YDMF_PHYS_NEXT_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(CPG_GPAR_TYPE),            INTENT(INOUT) :: YDCPG_GPAR
TYPE(CPG_PHY_TYPE),             INTENT(IN)    :: YDCPG_PHY0
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(CPG_DYN_TYPE),             INTENT(IN)    :: YDCPG_DYN0
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(CPG_SL2_TYPE),             INTENT(INOUT) :: YDCPG_SL2
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(TCFU),                     INTENT(IN)    :: YDCFU
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
TYPE(TYP_DDH),                  INTENT(INOUT) :: YDDDH
TYPE(TEPHYSMWAVE),              INTENT(INOUT) :: YDPHYSMWAVE 

#include "apl_arpege_part0.intfb.h"
#include "accldia.intfb.h"
#include "acclph.intfb.h"
#include "acdnshf.intfb.h"
#include "acdrag.intfb.h"
#include "acdrme.intfb.h"
#include "acevadcape.intfb.h"
#include "achmt.intfb.h"
#include "achmtls.intfb.h"
#include "acpluis.intfb.h"
#include "acsol.intfb.h"
#include "actqsat.intfb.h"
#include "acvisih.intfb.h"
#include "cpqsol.intfb.h"
#include "mf_phys_bayrad.intfb.h"
#include "mf_phys_corwat.intfb.h"
#include "mf_phys_cvv.intfb.h"
#include "mf_phys_fpl_part2.intfb.h"
#include "mf_phys_mocon.intfb.h"
#include "mf_phys_precips.intfb.h"
#include "mf_phys_save_phsurf_part2.intfb.h"
#include "mf_phys_transfer.intfb.h"
#include "ppwetpoint.intfb.h"
#include "qngcor.intfb.h"
#include "aplpar_flexdia.intfb.h"
#include "apl_arpege_oceanic_fluxes.intfb.h"
#include "apl_wind_gust.intfb.h"
#include "apl_arpege_shallow_convection_and_turbulence.intfb.h"
#include "apl_arpege_albedo_computation.intfb.h"
#include "apl_arpege_aerosols_for_radiation.intfb.h"
#include "apl_arpege_cloudiness.intfb.h"
#include "apl_arpege_radiation.intfb.h"
#include "apl_arpege_soil_hydro.intfb.h"
#include "apl_arpege_surface.intfb.h"
#include "apl_arpege_deep_convection.intfb.h"
#include "apl_arpege_turbulence_part2.intfb.h"
#include "apl_arpege_precipitation.intfb.h"
#include "apl_arpege_hydro_budget.intfb.h"
#include "apl_arpege_surface_update.intfb.h"
#include "apl_arpege_atmosphere_update.intfb.h"
#include "apl_arpege_init.intfb.h"
#include "apl_arpege_init_surfex.intfb.h"
#include "apl_arpege_dprecips.intfb.h"
#include "apl_arpege_mwave.intfb.h"


REAL(KIND=JPRB) :: ZDIFEXT(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)     ! Extra-GFL fluxes.
INTEGER(KIND=JPIM) :: INLAB_CVPP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZXTROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG),ZXUROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZMRIPP(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZKTROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG),ZNBVNO(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZKUROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG),ZKUROV2(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZKQROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG),ZKQLROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZNEBS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZQLIS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZNEBS0(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZQLIS0(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZNEBC0(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)          ! Nebulosite convective radiative
REAL(KIND=JPRB) :: ZNEBDIFF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)        ! Nebulosite: calcul de la diffusion
REAL(KIND=JPRB) :: ZNEBCH(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)          ! Nebulosite convective condensation
REAL(KIND=JPRB) :: ZUNEBH(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)          ! Nebulosite convective histo
REAL(KIND=JPRB) :: ZFPCOR(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZPOID(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)           ! DP/(YDMODEL%YRCST%RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
REAL(KIND=JPRB) :: ZQV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)             ! corrected (for negative values) vapour
REAL(KIND=JPRB) :: ZQI(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)             ! corrected (for negative values) cloud ice
REAL(KIND=JPRB) :: ZQL(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)             ! corrected (for negative values) cloud liquid
REAL(KIND=JPRB) :: ZQR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)             ! corrected (for negative values) rain
REAL(KIND=JPRB) :: ZQS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)             ! corrected (for negative values) snow
REAL(KIND=JPRB) :: ZTENHA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENQVA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZCP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)             ! new cp for turbulent diffusion
REAL(KIND=JPRB) :: ZFPLSL(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)        ! total liquid water flux: diff+sedi+rain
REAL(KIND=JPRB) :: ZFPLSN(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)        ! total solid water flux: diff+sedi+snow
REAL(KIND=JPRB) :: ZSEDIQL(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)       ! sedimentation flux of cloud liquid water
REAL(KIND=JPRB) :: ZSEDIQI(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)       ! sedimentation flux of cloud ice water
REAL(KIND=JPRB) :: ZDIFCVPPQ(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)     ! Flux de CVPP (KFB or EDKF) sur Qv
REAL(KIND=JPRB) :: ZDIFCVPPS(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)     ! Flux de CVPP (KFB or EDKF) sur CpT
REAL(KIND=JPRB) :: ZDIFCVPPU(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)     ! Flux de CVPP (EDKF) sur U
REAL(KIND=JPRB) :: ZDIFCVPPV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)     ! Flux de CVPP (EDKF) sur V
REAL(KIND=JPRB) :: ZEDMFQ(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)        ! Mass flux part of EDMF flux for Qv
REAL(KIND=JPRB) :: ZEDMFS(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)        ! Mass flux part of EDMF flux for CpT
REAL(KIND=JPRB) :: ZEDMFU(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)        ! Mass flux part of EDMF flux for U
REAL(KIND=JPRB) :: ZEDMFV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)        ! Mass flux part of EDMF flux for V
REAL(KIND=JPRB) :: ZMF_UP(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)        ! Mass flux for implicit formulation of EDMF equation (LEDMFI)
REAL(KIND=JPRB) :: ZCONDCVPPL(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)    ! Flux de condensation liquide du a CVVPP (KFB)
REAL(KIND=JPRB) :: ZCONDCVPPI(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)    ! Flux de condensation glace du a CVVPP (KFB)
REAL(KIND=JPRB) :: ZPRODTH_CVPP(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)  ! Flux de production thermique de TKE du a CVPP(KFB)
REAL(KIND=JPRB) :: ZPRODTH(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)         ! Flux de production de TKE 
REAL(KIND=JPRB) :: ZDE2MR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)          ! temporary array for conversion of density to mixing ratio
REAL(KIND=JPRB) :: ZXDROV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZXHROV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZUGST(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZVGST(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCDROV(YDCPG_OPTS%KLON)                            ! ZCDROV     : PCD RENORME EN DENSITE FOIS VITESSE.
REAL(KIND=JPRB) :: ZCHROV(YDCPG_OPTS%KLON)                            ! ZCHROV     : PCH RENORME EN DENSITE FOIS VITESSE.
REAL(KIND=JPRB) :: ZDQSTS(YDCPG_OPTS%KLON)                            ! ZDQSTS     : DERIVEE DE PQSATS PAR RAPPORT A LA TEMPERATURE.
REAL(KIND=JPRB) :: ZGWDCS(YDCPG_OPTS%KLON)                            ! ZGWDCS     : VARIABLE DE SURFACE POUR LE DRAG OROGRAPHIQUE (RHO*N0/G).
REAL(KIND=JPRB) :: ZHQ(YDCPG_OPTS%KLON)                               ! ZHQ        : POIDS DE L'HUMIDITE DE L'AIR DANS L'HUMIDITE DE SURFACE.
REAL(KIND=JPRB) :: ZHU(YDCPG_OPTS%KLON)                               ! ZHU        : POIDS DE L'HUMIDITE SATURANTE DANS L'HUMIDITE DE SURFACE.
REAL(KIND=JPRB) :: ZHTR(YDCPG_OPTS%KLON)                              ! ZHTR       : RESISTANCE A LA TRANSPIRATION DU COUVERT VEGETAL. /  FOLIAGE TRANSPIRATION RESISTANCE.
REAL(KIND=JPRB) :: ZRTI(YDCPG_OPTS%KLON)                              ! ZRTI       : INVERSE DE R*T.
REAL(KIND=JPRB) :: ZDPHI(YDCPG_OPTS%KLON)                             ! ZDPHI      : EPAISSEUR EN GEOPOTENTIEL DU NIVEAU DE SURFACE.
REAL(KIND=JPRB) :: ZPRS(YDCPG_OPTS%KLON)                              ! ZPRS       : CONSTANTE DES GAZ POUR L'AIR AU SOL.
REAL(KIND=JPRB) :: ZSTAB(YDCPG_OPTS%KLON)                             ! ZSTAB      : INDICE DE STABILITE A LA SURFACE.
REAL(KIND=JPRB) :: ZWFC(YDCPG_OPTS%KLON)                              ! ZWFC       : TENEUR EN EAU A LA CAPACITE AUX CHAMPS. /  FIELD CAPACITY WATER CONTENT.
REAL(KIND=JPRB) :: ZWPMX(YDCPG_OPTS%KLON)                             ! ZWPMX      : TENEUR EN EAU MAXIMALE DU RESERVOIR PROFOND.  / MAXIMUM WATER CONTENT OF THE DEEP WATER-TANK.
REAL(KIND=JPRB) :: ZWLMX(YDCPG_OPTS%KLON)                             ! ZWLMX      : TENEUR EN EAU MAXIMALE DU RESERVOIR D'INTERCEPTION. / MAXIMUM WATER CONTENT OF THE INTERCEPTION WATER-TANK.
REAL(KIND=JPRB) :: ZWSEQ(YDCPG_OPTS%KLON)                             ! ZWSEQ      : TENEUR EN EAU A L'EQUILIBRE (EQUILIBRE ENTRE GRAVITE ET CAPILLARITE) EN SURFACE.  / SURFACE WATER CONTENT FOR THE BALANCE BETWEEN GRAVITY AND CAPILLARITY
REAL(KIND=JPRB) :: ZWSMX(YDCPG_OPTS%KLON)                             ! ZWSMX      : TENEUR EN EAU MAXIMALE DU RESERVOIR SUPERFICIEL.  / MAXIMUM WATER CONTENT FOR THE SUPERFICIAL WATER-TANK.
REAL(KIND=JPRB) :: ZWWILT(YDCPG_OPTS%KLON)                            ! ZWWILT     : TENEUR EN EAU AU POINT DE FLETRISSEMENT.  / WATER CONTENT AT THE WILTING POINT.
REAL(KIND=JPRB) :: ZC3(YDCPG_OPTS%KLON)                               ! ZC3        : COEFFICIENT UTILE POUR LE CALCUL DU DRAINAGE
REAL(KIND=JPRB) :: ZCG(YDCPG_OPTS%KLON)                               ! ZCG        : COEFFICIENT THERMIQUE DU SOL NU.  / THERMICAL COEFFICIENT OF BARE GROUND.
REAL(KIND=JPRB) :: ZCN(YDCPG_OPTS%KLON)                               ! ZCN        : COEFFICIENT THERMIQUE DE LA NEIGE./ THERMICAL COEFFICIENT OF SNOW.
REAL(KIND=JPRB) :: ZNEIJG(YDCPG_OPTS%KLON)                            ! ZNEIJG     : FRACTION DE NEIGE RECOUVRANT LE SOL.
REAL(KIND=JPRB) :: ZNEIJV(YDCPG_OPTS%KLON)                            ! ZNEIJV     : FRACTION DE NEIGE RECOUVRANT LA VEGETATION.
REAL(KIND=JPRB) :: ZPCLS(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZBLH(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZQO3(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)          ! ZQO3    : RAPPORT DE MELANGE MASSIQUE D'OZONE MOYEN AU-DESSUS DU MODELE / OZONE MIXING RATIO (MASS) AVERAGED-ABOVE
REAL(KIND=JPRB) :: ZAER(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,6)
REAL(KIND=JPRB) :: ZAERINDS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZDPHIV(YDCPG_OPTS%KLON),ZDPHIT(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZTRSOD(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCEMTR(YDCPG_OPTS%KLON,0:1) , ZCTRSO(YDCPG_OPTS%KLON,0:1)
REAL(KIND=JPRB) :: ZALBD(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW), ZALBP(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB) :: ZSFSWDIR (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW), ZSFSWDIF (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB) :: ZTRSODIR (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW), ZTRSODIF (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB) :: ZSUDU(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB) :: ZTHETAVS(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZGEOSLC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTHETAV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZCOEFN(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)          ! ZCOEFN : COEFFICIENT STATISTIQUE POUR LES FLUX D'EAUX CONDENSEES.
REAL(KIND=JPRB) :: ZQCO2(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZALBDIR(YDCPG_OPTS%KLON) ! direct (parallel) surface albedo
REAL(KIND=JPRB) :: ZCLCT_RAD(YDCPG_OPTS%KLON) ! total cloud cover for radiation

! LOCAL ARRAYS FOR ACVPPKF
REAL(KIND=JPRB) :: ZQLI_CVPP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZNEB_CVPP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

! Implicit coupling coefficients
REAL(KIND=JPRB) :: ZCFBTH(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZDTMSE,ZRHGMT,ZSTATI
REAL(KIND=JPRB) :: ZCFATH(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZCFAU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZCFBU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZCFBV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZSRAIN(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZSSNOW(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZSGROUPEL(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZTSN(YDCPG_OPTS%KLON)

!         ACFLUSO (ECUME) local variable
REAL(KIND=JPRB) :: ZCE(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCEROV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCRTI(YDCPG_OPTS%KLON)

!        New ACDIFV1 local variable
REAL(KIND=JPRB) :: ZXURO(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZXQRO(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZXTRO(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)

!        New ACNORGWD local variables
REAL(KIND=JPRB) :: ZFLX_LOTT_GWU(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFLX_LOTT_GWV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)

!    TKE+ for ACCLDIA
REAL(KIND=JPRB) :: ZTKE1(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

!   For ACVISIH
REAL(KIND=JPRB) :: ZQGM(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)


!        New ARP_GROUND_PARAM local variable
REAL(KIND=JPRB) :: ZALPHA1(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZCOEFA (YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLVT   (YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZQICE  (YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZDIFWQ (YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZDIFWS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZSC_FEVI (YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZSC_FEVN(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZSC_FCLL(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZSC_FCLN(YDCPG_OPTS%KLON)

!           TRAJECTORY (For diffusion !) local VARIABLES
REAL(KIND=JPRB) :: ZTRAJGWD(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) !Traj buffer saved for TL/AD (YDMODEL%YRML_PHY_MF%YRSIMPHL%LGWDSPNL)

REAL(KIND=JPRB) :: ZAIPCMT(YDCPG_OPTS%KLON) ! Activity Index of PCMT: 1. if PCMT is active, 0. else case.
REAL(KIND=JPRB) :: ZQIC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG),ZQLC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZQLI_CVP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZQC_DET_PCMT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZDCAPE(YDCPG_OPTS%KLON) ! Descending CAPE for gusts.

! ACRANEB/ACRANEB2 local variables
REAL(KIND=JPRB) :: ZDECRD   (YDCPG_OPTS%KLON) ! decorrelation depth for cloud overlaps

! Precipitation type diagnostics
INTEGER (KIND=JPIM)  :: IMOC_CLPH (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZBAY_QRCONV (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZBAY_QSCONV (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZDSA_C1 (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZDSA_C2 (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZDSA_CPS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZDSA_LHS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZDSA_RS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_CDN (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_CD (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_CHN (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_CH (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_EMIS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_FEVI (YDCPG_OPTS%KLON, 1:YDMODEL%YRML_PHY_G%YRDPHY%NTSSG+1)
REAL(KIND=JPRB)     :: ZFLU_NEIJ (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_QSATS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFLU_QSAT (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZFLU_VEG (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZMSC_FRMQ (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZFCHQ (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZMSC_LH (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZMSC_LSCPE (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZMSC_QW (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZMSC_TW (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPFL_FEFB1 (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPFL_FEFB2 (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPFL_FEFB3 (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPFL_FPLCH (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPFL_FPLSH (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPFL_FTKEI (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPFL_FTKE (YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZPRC_DPRECIPS2 (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%NDTPREC2)
REAL(KIND=JPRB)     :: ZPRC_DPRECIPS  (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%NDTPREC)
REAL(KIND=JPRB)     :: ZRDG_CVGQ (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZRDG_CVGT (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZRDG_LCVQ (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZRDG_MU0LU (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZRDG_MU0M (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZRDG_MU0N (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZRDG_MU0 (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZRDG_SOLO(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_DDAL (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZSAV_DDOM (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZSAV_ENTCH (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZSAV_FHPS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_GZ0F (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_GZ0HF (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_HV (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_PBLH (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_QSH  (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_UDAL (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZSAV_UDGRO (YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSAV_UDOM (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZSAV_UNEBH (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)

INTEGER(KIND=JPIM) :: JLEV, JLON
LOGICAL :: LLCLS, LLHMT, LLREDPR
REAL(KIND=JPRB) :: ZEPS0, ZEPSNEB
REAL(KIND=JPRB) :: ZRVMD

! Dissipation length for actke1 et actke2
REAL(KIND=JPRB) :: ZUSLE(YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZKCLS(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZECTCLS(YDCPG_OPTS%KLON)

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('APL_ARPEGE',0,ZHOOK_HANDLE)

ASSOCIATE (KLON => YDCPG_OPTS%KLON, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA, KFLEVG => YDCPG_OPTS%KFLEVG)

ASSOCIATE(YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY, YDTOPH=>YDMODEL%YRML_PHY_MF%YRTOPH, YDRIP=>YDMODEL%YRML_GCONF%YRRIP,             &
& YDARPHY=>YDMODEL%YRML_PHY_MF%YRARPHY, YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY, YDPHY2=>YDMODEL%YRML_PHY_MF%YRPHY2,               &
& YGFL=>YDMODEL%YRML_GCONF%YGFL, YDSTA=>YDGEOMETRY%YRSTA, YDMCC=>YDMODEL%YRML_AOC%YRMCC, YDPHY3=>YDMODEL%YRML_PHY_MF%YRPHY3, &
& YDSPP_CONFIG=>YDMODEL%YRML_GCONF%YRSPP_CONFIG, YDEPHY=> YDMODEL%YRML_PHY_EC%YREPHY,                                        &
& YDPHY1=>YDMODEL%YRML_PHY_MF%YRPHY1, YDPHY0=>YDMODEL%YRML_PHY_MF%YRPHY0)


ASSOCIATE(TSPHY=>YDPHY2%TSPHY, NTSSG=>YDDPHY%NTSSG, LMSE=>YDARPHY%LMSE, LNEBN=>YDPHY%LNEBN, NDPSFI=>YDPHY%NDPSFI, &
& LRRGUST=>YDPHY%LRRGUST, LEDR=>YDPHY%LEDR, NTPLUI=>YDTOPH%NTPLUI, XMINLM=>YDPHY0%XMINLM, XMAXLM=>YDPHY0%XMAXLM,  &
& HSOLIWR=>YDPHY1%HSOLIWR, WSMX=>YDPHY1%WSMX, HSOLIT0=>YDPHY1%HSOLIT0, HSOL=>YDPHY1%HSOL, WPMX=>YDPHY1%WPMX,      &
& LRAFTKE=>YDPHY2%LRAFTKE, HVCLS=>YDPHY2%HVCLS, HTCLS=>YDPHY2%HTCLS, RII0=>YDPHY3%RII0, YA=>YGFL%YA,              &
& YIRAD=>YGFL%YIRAD, YLRAD=>YGFL%YLRAD, LSTRAS=>YDPHY%LSTRAS, LSOLV=>YDPHY%LSOLV, NTDRME=>YDTOPH%NTDRME,          &
& NTDRAG=>YDTOPH%NTDRAG, NTQSAT=>YDTOPH%NTQSAT, LMCC03=>YDMCC%LMCC03, RHGMT=>YDRIP%RHGMT, RSTATI=>YDRIP%RSTATI,   &
& LGCHECKMV=>YDPHY%LGCHECKMV, LECTPRODCVP=>YDPHY%LECTPRODCVP, LCVTDK=>YDPHY%LCVTDK, LCVPPKF=>YDPHY%LCVPPKF,       &
& TSTEP=>YDRIP%TSTEP, LEDKF=>YDPHY%LEDKF, LEDMFI=>YDPHY%LEDMFI)


ASSOCIATE(PAPRSF=> YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF, PAPRS => YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,                                   &
& PAPHIF=> YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, PAPHI=> YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI, PDELP => YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, &
& PR=>YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%R, PT=> YDMF_PHYS_BASE_STATE%T, PU=> YDMF_PHYS_BASE_STATE%U,                                       &
& PV=>YDMF_PHYS_BASE_STATE%V, PCP=>YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP)

LLREDPR=.FALSE.
ZRVMD=YDMODEL%YRCST%RV-YDMODEL%YRCST%RD

IF (YDCPG_OPTS%LCONFX) THEN
  ZDTMSE=0.01_JPRB
  ZSTATI=RSTATI+(ZDTMSE-TSTEP)*0.5_JPRB
ELSE
  ZDTMSE=TSPHY
  ZSTATI=RSTATI
ENDIF
ZRHGMT=REAL(RHGMT,JPRB)

CALL APL_ARPEGE_PART0(YDMF_PHYS_BASE_STATE, YDMF_PHYS_NEXT_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS,           &
& YDCPG_MISC, YDCPG_GPAR, YDCPG_PHY0, YDMF_PHYS, YDCPG_DYN0, YDMF_PHYS_SURF, YDCPG_SL2, YDVARS, YDMODEL, YDDDH, &
& YDPHYSMWAVE, IMOC_CLPH, ZDSA_C1, ZDSA_C2, ZDSA_CPS, ZDSA_LHS, ZDSA_RS, ZFLU_CDN, ZFLU_CD, ZFLU_CHN, ZFLU_CH,  &
& ZFLU_EMIS, ZFLU_FEVI, ZFLU_NEIJ, ZFLU_QSATS, ZFLU_QSAT, ZFLU_VEG, ZMSC_FRMQ, ZFCHQ, ZMSC_LH, ZMSC_LSCPE,      &
& ZMSC_QW, ZMSC_TW, ZPFL_FEFB1, ZPFL_FEFB2, ZPFL_FEFB3, ZPFL_FPLCH, ZPFL_FPLSH, ZPFL_FTKEI, ZPFL_FTKE,          &
& ZRDG_CVGQ, ZRDG_CVGT, ZRDG_LCVQ, ZRDG_MU0LU, ZRDG_MU0M, ZRDG_MU0N, ZRDG_MU0, ZRDG_SOLO, ZSAV_DDAL, ZSAV_DDOM, &
& ZSAV_ENTCH, ZSAV_FHPS, ZSAV_GZ0F, ZSAV_GZ0HF, ZSAV_HV, ZSAV_PBLH, ZSAV_QSH , ZSAV_UDAL, ZSAV_UDGRO, ZSAV_UDOM,&
& ZSAV_UNEBH, ZQCO2)

CALL APL_ARPEGE_INIT (YDMODEL%YRCST, YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC,         &
& YDMF_PHYS, YDMF_PHYS_SURF, YDVARS, YDMODEL, IMOC_CLPH, INLAB_CVPP, ZAER, ZAERINDS, ZAIPCMT, ZALBD,   &
& ZALBP, ZALPHA1, ZCEMTR, ZCFATH, ZCFAU, ZCFBTH, ZCFBU, ZCFBV, ZCOEFA, ZCONDCVPPI, ZCONDCVPPL,         &
& ZCTRSO, ZDECRD, ZDIFCVPPQ, ZDIFCVPPS, ZDIFCVPPU, ZDIFCVPPV, ZDIFWQ, ZDIFWS, ZEDMFQ, ZEDMFS, ZEDMFU,  &
& ZEDMFV, ZEPS0, ZEPSNEB, ZFPCOR, ZKQLROV, ZKQROV, ZKTROV, ZKUROV, ZLVT, ZMF_UP, ZMRIPP, ZNEBC0,       &
& ZNEBCH, ZNEBDIFF, ZNEBS, ZNEBS0, ZNEB_CVPP, ZPFL_FPLCH, ZPFL_FPLSH, ZPOID, ZPRODTH_CVPP,             &
& ZQC_DET_PCMT, ZQI, ZQIC, ZQICE, ZQL, ZQLC, ZQLIS, ZQLIS0, ZQLI_CVP, ZQLI_CVPP, ZQO3, ZQR, ZQS, ZQV,  &
& ZSC_FCLL, ZSC_FCLN, ZSC_FEVI, ZSC_FEVN, ZSEDIQI, ZSEDIQL, ZSFSWDIF, ZSFSWDIR, ZSUDU, ZTENHA,         &
& ZTENQVA, ZTENT, ZTRSOD, ZTRSODIF, ZTRSODIR, ZUNEBH, ZXDROV, ZXHROV, ZXQRO, ZXTRO, ZXTROV, ZXURO,     &
& ZXUROV)

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACTQSAT {

CALL ACTQSAT (YDMODEL%YRCST, YDPHY, KIDIA, KFDIA, KLON, NTQSAT, KFLEVG, &
& PAPRSF, PCP, ZQV, PT, ZGEOSLC, ZMSC_LH, ZMSC_LSCPE, ZFLU_QSAT, ZMSC_QW, YDCPG_MISC%RH, ZMSC_TW)

!$ACDC }
  

IF ( .NOT.LMSE ) THEN

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACSOL {

  IF ( LSOLV ) THEN
    LLHMT=.FALSE.
    CALL ACSOL (YDCPG_OPTS%YRCLI, YDMODEL%YRCST, YDPHY, YDPHY1, KIDIA, KFDIA, KLON,                                        &
    & YDMF_PHYS_SURF%GSD_VV%PARG, YDMF_PHYS_SURF%GSD_VV%PD2, YDMF_PHYS_SURF%GSD_VF%PZ0F, YDMF_PHYS_SURF%GSD_VV%PZ0H,       &
    & YDMF_PHYS_SURF%GSD_VF%PZ0RLF, YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_SURF%GSD_VV%PIVEG, YDMF_PHYS_SURF%GSD_VV%PLAI,   &
    & YDMF_PHYS_BASE_STATE%YGSP_SG%A (:,1), YDMF_PHYS_BASE_STATE%YGSP_SG%R (:,1), YDMF_PHYS_SURF%GSD_VV%PSAB,              &
    & YDMF_PHYS_BASE_STATE%YGSP_SG%F (:,1), YDMF_PHYS_BASE_STATE%YGSP_RR%T, YDMF_PHYS_SURF%GSD_VF%PVEG,                    &
    & YDMF_PHYS_BASE_STATE%YGSP_SB%Q (:,1), YDMF_PHYS_BASE_STATE%YGSP_SB%TL (:,1), YDMF_PHYS_BASE_STATE%YGSP_RR%W,         &
    & YDMF_PHYS_BASE_STATE%YGSP_RR%IC, LLHMT, ZDSA_C1, ZDSA_C2, ZC3, ZCG, ZCN, YDMF_PHYS%OUT%CT, ZNEIJG, ZNEIJV, ZWFC,     &
    & ZWPMX, ZWSEQ, ZWSMX, ZWWILT)
  ELSE

!DEC$ IVDEP
    DO JLON = KIDIA, KFDIA
      YDMF_PHYS%OUT%CT(JLON)=HSOL /(&
      & 1.0_JPRB+HSOLIWR*(YDMF_PHYS_BASE_STATE%YGSP_RR%W(JLON)+YDMF_PHYS_BASE_STATE%YGSP_SB%Q(JLON,1))/(WSMX+WPMX)&
      & *EXP(-0.5_JPRB*(HSOLIT0*(YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON)-YDMODEL%YRCST%RTT))**2))
    ENDDO
  ENDIF

!$ACDC }

ENDIF


CALL APL_ARPEGE_INIT_SURFEX (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC, &
& YDCPG_GPAR, YDMF_PHYS, YDVARS, YDMODEL, ZALBD, ZALBP, ZFLU_EMIS, ZSGROUPEL, ZSRAIN, ZSSNOW, ZTSN)

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ZDPHIV {

DO JLON = KIDIA, KFDIA
  ZDPHIV(JLON)=YDMODEL%YRCST%RG*HVCLS
  ZDPHIT(JLON)=YDMODEL%YRCST%RG*HTCLS
ENDDO

!$ACDC }
  
LLCLS=.TRUE.
LLHMT=.TRUE.
IF (LMSE) THEN      

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACHMTLS {

  CALL ACHMTLS (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF, KIDIA, KFDIA, KLON,                            &
  & KFLEVG, PAPHI, PAPHIF, PAPRS, PAPRSF, PR, PT, PU, PV, YDMF_PHYS_BASE_STATE%YGSP_RR%T,          &
  & YDCPG_MISC%QS, ZDPHIT, YDMF_PHYS%OUT%GZ0, YDMF_PHYS%OUT%GZ0H, LLCLS, ZNBVNO, ZMRIPP, ZDSA_CPS, &
  & ZGWDCS, ZDSA_LHS, ZPCLS, ZFLU_CD, ZFLU_CDN)
  DO JLON = KIDIA, KFDIA
    ZDPHI(JLON)=PAPHIF(JLON,KFLEVG)-PAPHI(JLON,KFLEVG)
    ZPRS(JLON)=YDMODEL%YRCST%RD+ZRVMD*YDCPG_MISC%QS(JLON)
    ZRTI(JLON)=2.0_JPRB/(PR(JLON,KFLEVG)*PT(JLON,KFLEVG)+YDMODEL%YRCST%RKAPPA*ZDPHI(JLON)&
    & +ZPRS(JLON)*YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON))
  ENDDO      

!$ACDC }

ELSE      

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACHMT {

  CALL ACHMT (YDCPG_OPTS%YRCLI, YDMODEL%YRML_PHY_MF%YRPHY, YDMODEL%YRML_PHY_MF%YRPHY0, YDMODEL%YRML_PHY_MF%YRPHY1,     &
  & YDMODEL%YRML_PHY_MF%YRPHY2, YDMODEL%YRCST, KIDIA, KFDIA, KLON, KFLEVG,                                             &
  & YDCPG_OPTS%YRSURF_DIMS%YSD_VVD%NUMFLDS>=8.AND.LSOLV, PAPHI, PAPHIF, PAPRS, PAPRSF, PCP, ZQV, PR,                   &
  & PT, PU, PV, ZPFL_FPLSH, ZPFL_FPLCH, ZDPHIT, ZDPHIV, YDMF_PHYS_SURF%GSD_VF%PZ0F, YDMF_PHYS_SURF%GSD_VV%PZ0H,        &
  & YDMF_PHYS_SURF%GSD_VF%PZ0RLF, YDMF_PHYS_SURF%GSD_VV%PHV, YDMF_PHYS_SURF%GSD_VF%PLSM, ZNEIJG,                       &
  & ZNEIJV, YDMF_PHYS_BASE_STATE%YGSP_SG%F (:,1), YDMF_PHYS_BASE_STATE%YGSP_RR%T, YDMF_PHYS_SURF%GSD_VF%PVEG,          &
  & ZWFC, YDMF_PHYS_BASE_STATE%YGSP_RR%W, YDMF_PHYS_BASE_STATE%YGSP_RR%IC, LLCLS, LLHMT, ZNBVNO,                       &
  & ZMRIPP, ZFLU_CD, ZFLU_CDN, ZCDROV, ZFLU_CH, ZCHROV, ZDSA_CPS, ZDQSTS, ZGWDCS, YDMF_PHYS%OUT%GZ0,                   &
  & YDMF_PHYS%OUT%GZ0H, ZHQ, ZHU, ZFLU_NEIJ, YDMF_PHYS%OUT%QCLS, YDCPG_MISC%QS, ZFLU_QSATS, YDMF_PHYS%OUT%RHCLS,       &
  & ZDSA_RS, ZRTI, ZSTAB, YDMF_PHYS%OUT%TCLS, YDMF_PHYS%OUT%UCLS, YDMF_PHYS%OUT%VCLS, YDMF_PHYS%OUT%NUCLS,             &
  & YDMF_PHYS%OUT%NVCLS, ZPCLS, ZFLU_VEG, ZXDROV, ZXHROV, YDMF_PHYS%OUT%UGST, YDMF_PHYS%OUT%VGST)

!$ACDC }

ENDIF
    

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACCLPH {

DO JLEV = 1, KFLEVG
  DO JLON = KIDIA, KFDIA
    ZCP (JLON, JLEV) = PCP (JLON, JLEV)
  ENDDO
ENDDO

DO JLEV=YDCPG_OPTS%KTDIA,KFLEVG
  DO JLON = KIDIA, KFDIA
    ZTHETAV(JLON,JLEV)=PT(JLON,JLEV)*(1.0_JPRB+YDMODEL%YRCST%RETV*ZQV(JLON,JLEV))&
    & *(YDMODEL%YRCST%RATM/PAPRSF(JLON,JLEV))**YDMODEL%YRCST%RKAPPA  
  ENDDO
ENDDO

DO JLON = KIDIA, KFDIA
  ZTHETAVS(JLON)=YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON)*(1.0_JPRB+YDMODEL%YRCST%RETV*YDCPG_MISC%QS(JLON))&
  & *(YDMODEL%YRCST%RATM/PAPRS(JLON,KFLEVG))**YDMODEL%YRCST%RKAPPA  
ENDDO

CALL ACCLPH (YDMODEL%YRCST, YDPHY0, YDPHY2, KIDIA, KFDIA, KLON, YDCPG_OPTS%KTDIA, &
& KFLEVG, ZTHETAV, PAPHI, PAPHIF, PU, PV, ZTHETAVS, IMOC_CLPH, YDMF_PHYS%OUT%CLPH, YDMF_PHYS%OUT%VEIN,  &
& ZUGST, ZVGST)


DO JLON = KIDIA, KFDIA
  ZBLH(JLON) = YDMF_PHYS%OUT%CLPH(JLON)
ENDDO

!$ACDC }

CALL APL_ARPEGE_OCEANIC_FLUXES (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, YDMF_PHYS_SURF, &
& YDMODEL, LLHMT, ZCDROV, ZCEROV, ZCHROV, ZDPHIT, ZDPHIV, ZDSA_RS, ZFLU_CD, ZFLU_CDN, ZFLU_CH, ZFLU_QSATS)


CALL APL_WIND_GUST (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, YDVARS, &
& YDMODEL, IMOC_CLPH, ZBLH, ZDCAPE)

CALL APL_ARPEGE_SHALLOW_CONVECTION_AND_TURBULENCE (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS,              &
& YDCPG_MISC, YDMF_PHYS, YDMF_PHYS_SURF, YDMODEL, YDDDH, INLAB_CVPP, ZCDROV, ZCHROV, ZCOEFN, ZCONDCVPPI,      &
& ZCONDCVPPL, ZDIFCVPPQ, ZDIFCVPPS, YDMODEL%YRML_PHY_MF%YRPHY0%REPS, ZFLU_CD, ZFLU_CH, ZKQLROV,               &
& ZKQROV, ZKTROV, ZKUROV, ZKUROV2, ZMSC_LSCPE, ZNBVNO, ZNEBS, ZNEBS0, ZNEB_CVPP, ZPFL_FPLCH, ZPFL_FTKE,       &
& ZPFL_FTKEI, ZPRODTH_CVPP, ZQI, ZQIC, ZQL, ZQLC, ZQLIS, ZQLIS0, ZQLI_CVPP, ZQV, ZQR, ZQS, ZTKE1, ZXTROV,     &
& ZXUROV, ZUSLE, ZKCLS, ZECTCLS, ZPRODTH, ZEDMFQ, ZEDMFS, ZEDMFU, ZEDMFV, ZMF_UP)

CALL APL_ARPEGE_ALBEDO_COMPUTATION (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, &
& YDMF_PHYS_SURF, YDMODEL, ZALBD, ZALBP, ZEPS0, ZFLU_EMIS, ZFLU_NEIJ, ZRDG_MU0, ZALBDIR)

CALL APL_ARPEGE_AEROSOLS_FOR_RADIATION (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS_SURF, &
& YDMODEL, ZAER, ZAERINDS)

CALL APL_ARPEGE_CLOUDINESS (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC, YDMF_PHYS,      &
& YDVARS, YDMODEL, LLREDPR, ZAIPCMT, ZBLH, ZDECRD, ZFLU_QSAT, ZMSC_QW, ZNEBC0, ZNEBCH, ZNEBS, ZNEBS0, &
& ZNEB_CVPP, ZPFL_FPLCH, ZQI, ZQL, ZQLIS, ZQLIS0, ZQLI_CVP, ZQLI_CVPP, ZQV, ZUNEBH, ZCLCT_RAD, YDSTA)
  
CALL APL_ARPEGE_RADIATION (YDMF_PHYS_BASE_STATE, YDCFU, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC, &
& YDCPG_GPAR, YDMF_PHYS, YDMF_PHYS_SURF, YDVARS, YDMODEL, ZCLCT_RAD, ZALBDIR, ZDECRD, ZAER, ZAERINDS,   &
& ZALBD, ZALBP, ZQCO2, ZCEMTR, ZCTRSO, ZFLU_EMIS, ZFLU_QSAT, ZQO3, ZQR, ZQS, ZQV, ZRDG_MU0, ZRDG_SOLO,  &
& ZRDG_MU0LU, ZRDG_MU0M, ZSFSWDIF, ZSFSWDIR, ZSUDU, ZTENT, ZTRSOD, ZTRSODIF, ZTRSODIR)

CALL APL_ARPEGE_SOIL_HYDRO (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, YDMF_PHYS_SURF, &
& YDMODEL, ZCHROV, ZFLU_NEIJ, ZFLU_QSAT, ZFLU_QSATS, ZFLU_VEG, ZGWDCS, ZHQ, ZHTR, ZHU, ZQV, ZWFC,    &
& ZWLMX, ZWWILT)

CALL APL_ARPEGE_SURFACE (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC,       &
& YDCPG_GPAR, YDMF_PHYS, YDMF_PHYS_SURF, YDVARS, YDMODEL, ZALBD, ZALBP, ZALPHA1, ZCDROV, ZCEROV,     &
& ZCFATH, ZCFAU, ZCFBTH, ZCFBU, ZCFBV, ZCHROV, ZCOEFA, ZCOEFN, ZCP, ZDIFEXT, ZDIFWQ, ZDIFWS, ZDQSTS, &
& ZDSA_CPS, ZDSA_LHS, ZDTMSE, ZEDMFQ, ZEDMFS, ZEDMFU, ZEDMFV, ZFLU_CD, ZFLU_CDN, ZFLU_EMIS,          &
& ZFLU_FEVI, ZFLU_NEIJ, ZFLU_QSATS, ZFLU_VEG, ZHQ, ZHTR, ZHU, ZKQLROV, ZKQROV, ZKTROV, ZKUROV, ZLVT, &
& ZMF_UP, ZNEBCH, ZNEBDIFF, ZNEBS, ZPOID, ZQI, ZQICE, ZQL, ZQV, ZRDG_MU0, ZRDG_MU0N, ZRHGMT,         &
& ZSC_FCLL, ZSC_FCLN, ZSC_FEVI, ZSC_FEVN, ZSFSWDIF, ZSFSWDIR, ZSGROUPEL, ZSRAIN, ZSSNOW, ZSTATI,     &
& ZTSN, ZXDROV, ZXHROV, ZXQRO, ZXTRO, ZXTROV, ZXURO, ZXUROV)

! The deep convection will see the shallow part from KFB as it is with Louis scheme and the modified RI
    
!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=DIFTQ {

IF (LCVPPKF.OR.(LEDKF .AND. .NOT. LEDMFI)) THEN
   IF (LCVPPKF) THEN
      DO JLEV=YDCPG_OPTS%KTDIA,KFLEVG
         DO JLON = KIDIA, KFDIA
            YDMF_PHYS%OUT%DIFTQ   (JLON,JLEV) = YDMF_PHYS%OUT%DIFTQ(JLON,JLEV) + ZDIFCVPPQ(JLON,JLEV)
            YDMF_PHYS%OUT%DIFTS   (JLON,JLEV) = YDMF_PHYS%OUT%DIFTS(JLON,JLEV) + ZDIFCVPPS(JLON,JLEV)
            YDMF_PHYS%OUT%STRTU   (JLON,JLEV) = YDMF_PHYS%OUT%STRTU(JLON,JLEV) + ZDIFCVPPU(JLON,JLEV)
            YDMF_PHYS%OUT%STRTV   (JLON,JLEV) = YDMF_PHYS%OUT%STRTV(JLON,JLEV) + ZDIFCVPPV(JLON,JLEV)
         ENDDO
      ENDDO
    ELSEIF(LEDKF) THEN  
      DO JLEV=YDCPG_OPTS%KTDIA,KFLEVG
         DO JLON = KIDIA, KFDIA
            YDMF_PHYS%OUT%DIFTQ   (JLON,JLEV) = YDMF_PHYS%OUT%DIFTQ(JLON,JLEV) + ZEDMFQ(JLON,JLEV)
            YDMF_PHYS%OUT%DIFTS   (JLON,JLEV) = YDMF_PHYS%OUT%DIFTS(JLON,JLEV) + ZEDMFS(JLON,JLEV)
            YDMF_PHYS%OUT%STRTU   (JLON,JLEV) = YDMF_PHYS%OUT%STRTU(JLON,JLEV) + ZEDMFU(JLON,JLEV)
            YDMF_PHYS%OUT%STRTV   (JLON,JLEV) = YDMF_PHYS%OUT%STRTV(JLON,JLEV) + ZEDMFV(JLON,JLEV)
         ENDDO
      ENDDO
    ENDIF
ENDIF  
!$ACDC }

! Additional diagnostics of radiative fluxes (PFRTHDS and PFRSOPT)

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=FRSOPT {

!DEC$ IVDEP
DO JLON = KIDIA, KFDIA
  IF(.NOT.LMSE)YDMF_PHYS%OUT%FRTHDS(JLON)=YDMF_PHYS%OUT%FRTH(JLON,KFLEVG,1)/ZFLU_EMIS(JLON)+YDMODEL%YRCST%RSIGMA*YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON)**4
  IF(ZRDG_MU0(JLON) <= 0.0_JPRB) THEN
    YDMF_PHYS%OUT%FRSOPT(JLON)=0.0_JPRB
  ELSE
    YDMF_PHYS%OUT%FRSOPT(JLON)=RII0*ZRDG_MU0(JLON)
  ENDIF
ENDDO

!$ACDC }

! Additional diagnostic of the derivative of the non solar surface; heat flux with respect to surface temperature 

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACDRAG {

IF(LMCC03)THEN
  CALL ACDNSHF(YDMODEL%YRCST, YDPHY, YDPHY1, KIDIA, KFDIA, KLON, YDCPG_OPTS%KTDIA,                                &
  & KFLEVG, ZFLU_EMIS, YDMF_PHYS_SURF%GSD_VF%PLSM, ZFLU_NEIJ, ZQV, YDCPG_MISC%QS, YDMF_PHYS_BASE_STATE%YGSP_RR%T, &
  & ZCHROV, ZDQSTS, YDMF_PHYS%OUT%DRNSHF)
ENDIF

CALL ACDRAG (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF, KIDIA, KFDIA, KLON,                                    &
& NTDRAG, KFLEVG, PAPRS, PAPRSF, PDELP, ZNBVNO, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP,                &
& PU, PV, YDVARS%GEOMETRY%RCORI%T0, YDMF_PHYS_SURF%GSD_VF%PGETRL, ZGWDCS, YDMF_PHYS_SURF%GSD_VF%PVRLAN, &
& YDMF_PHYS_SURF%GSD_VF%PVRLDI, YDMF_PHYS%OUT%STRDU, YDMF_PHYS%OUT%STRDV, ZTRAJGWD)
  
IF ( LSTRAS ) THEN
  CALL ACPLUIS (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF, KIDIA, KFDIA, KLON,                                          &
  & NTPLUI, KFLEVG, PAPRSF, PDELP, ZNEBS, ZQV, ZQLIS, ZMSC_QW, PR, PT, YDMF_PHYS%OUT%FCSQL,                      &
  & YDMF_PHYS%OUT%FCSQN, YDMF_PHYS%OUT%FPLSL, YDMF_PHYS%OUT%FPLSN, YDMF_PHYS%OUT%FPEVPSL, YDMF_PHYS%OUT%FPEVPSN, &
  & YDMF_PHYS%OUT%FPFPSL, YDMF_PHYS%OUT%FPFPSN                                                                   &
  &                                                                       )
ENDIF

!$ACDC }
  
IF (LCVTDK) THEN  
  CALL APL_ARPEGE_DEEP_CONVECTION (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, &
  & YDCPG_DYN0, YDMF_PHYS_SURF, YDVARS, YDMODEL, ZFPCOR, ZQV, ZTENHA, ZTENQVA, ZTENT, ZRDG_CVGQ, ZRDG_CVGT)
ENDIF

IF (LECTPRODCVP) THEN

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=TURBULENCE_PART2 {

  CALL APL_ARPEGE_TURBULENCE_PART2(YDMODEL%YRCST, YDMODEL%YRML_PHY_MF, KIDIA, KFDIA,                            &
  & KLON, YDMODEL%YRML_PHY_MF%YRTOPH%NTCOEF, YDMODEL%YRML_PHY_MF%YRTOPH%NTCOET, KFLEVG,                         &
  & PAPHI, PAPHIF, PAPRS, PAPRSF, PDELP, PR, PCP, PT, YDMF_PHYS_BASE_STATE%Q, PU, PV, YDMF_PHYS_BASE_STATE%TKE, &
  & ZPRODTH, ZKUROV2, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCS, YDMF_PHYS%OUT%STRCU, YDMF_PHYS%OUT%STRCV,       &
  & ZUSLE, ZKCLS, ZECTCLS, ZPFL_FTKE, ZTKE1, YDMF_PHYS%OUT%EDR)

!$ACDC }

ENDIF

CALL APL_ARPEGE_PRECIPITATION (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS,        &
& YDMF_PHYS_SURF, YDVARS, YDMODEL, ZFLU_NEIJ, ZNEBS, ZNEB_CVPP, ZQC_DET_PCMT, ZQI, ZQL, ZQLIS, &
& ZQLI_CVPP, ZQR, ZQS, ZQV, ZSEDIQI, ZSEDIQL, ZTENHA, ZTENQVA, YDSTA)
    

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=QNGCOR {

DO JLEV = 0, KFLEVG
  DO JLON = KIDIA, KFDIA
    ZFLX_LOTT_GWU(JLON, JLEV) = 0.0_JPRB
    ZFLX_LOTT_GWV(JLON, JLEV) = 0.0_JPRB
  ENDDO
ENDDO

IF(LNEBN.OR.LRRGUST) THEN
  DO JLEV=YDCPG_OPTS%KTDIA-1,KFLEVG
    DO JLON = KIDIA, KFDIA
      ZPFL_FPLCH(JLON,JLEV)=ZFPCOR(JLON,JLEV)
    ENDDO
  ENDDO    
ENDIF

IF(LRRGUST) THEN
  DO JLEV=YDCPG_OPTS%KTDIA-1,KFLEVG
    DO JLON = KIDIA, KFDIA
      ZPFL_FPLSH(JLON,JLEV)=YDMF_PHYS%OUT%FPLSL(JLON,JLEV)+YDMF_PHYS%OUT%FPLSN(JLON,JLEV)+YDMF_PHYS%OUT%FPLSG(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF


! Update transport fluxes due to sedimentation of clouds.

DO JLEV=YDCPG_OPTS%KTDIA,KFLEVG
  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%DIFTQL(JLON,JLEV)=YDMF_PHYS%OUT%DIFTQL(JLON,JLEV)+ZSEDIQL(JLON,JLEV)
    YDMF_PHYS%OUT%DIFTQN(JLON,JLEV)=YDMF_PHYS%OUT%DIFTQN(JLON,JLEV)+ZSEDIQI(JLON,JLEV)
    ZFPLSL (JLON,JLEV)=YDMF_PHYS%OUT%DIFTQL(JLON,JLEV)+YDMF_PHYS%OUT%FPLSL (JLON,JLEV)
    ZFPLSN (JLON,JLEV)=YDMF_PHYS%OUT%DIFTQN(JLON,JLEV)+YDMF_PHYS%OUT%FPLSN (JLON,JLEV)
  ENDDO
ENDDO
  
! Correct negative water contents.
  
CALL QNGCOR (YDMODEL%YRCST, YDPHY2, KIDIA, KFDIA, KLON, NTPLUI, KFLEVG, &
& ZQV, ZQL, ZQI, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCQN,                &
& YDMF_PHYS%OUT%DIFCQL, YDMF_PHYS%OUT%DIFTQ, YDMF_PHYS%OUT%DIFTQN, YDMF_PHYS%OUT%DIFTQL, YDMF_PHYS%OUT%FPEVPSL,     &
& YDMF_PHYS%OUT%FPEVPSN, YDMF_PHYS%OUT%FPEVPCL, YDMF_PHYS%OUT%FPEVPCN, YDMF_PHYS%OUT%FPFPSL, YDMF_PHYS%OUT%FPFPSN,  &
& YDMF_PHYS%OUT%FPFPCL, YDMF_PHYS%OUT%FPFPCN, YDMF_PHYS%OUT%FCCQL, YDMF_PHYS%OUT%FCCQN, YDMF_PHYS%OUT%FCSQL,        &
& YDMF_PHYS%OUT%FCSQN, YDMF_PHYS%OUT%FCQNG )

!$ACDC }

! Bilan hydrique du sol

CALL APL_ARPEGE_HYDRO_BUDGET (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDCPG_GPAR, YDMF_PHYS, &
& YDMF_PHYS_SURF, YDMODEL, ZC3, ZCN, ZDSA_C1, ZDSA_C2, ZFLU_FEVI, ZFLU_NEIJ, ZFLU_VEG, ZFPLSL,     &
& ZFPLSN, ZWFC, ZWLMX, ZWPMX, ZWSEQ, ZWSMX)

! Drag mesospherique pour un modele possedant des niveaux au-dessus de 50 km (i.e. dans la mesosphere)

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACDRME {

CALL ACDRME ( YDMODEL%YRCST, YDSTA, YDPHY2, YDTOPH, KIDIA, KFDIA, KLON,                            &
& NTDRME, KFLEVG, PCP, PDELP, PT, ZQV, PU, PV, YDMF_PHYS%OUT%FRMH, ZMSC_FRMQ, YDMF_PHYS%OUT%STRMU, &
& YDMF_PHYS%OUT%STRMV)
  
! Store radiative cloudiness in GFL structure for ISP, Historical files or PostProcessing

IF (YIRAD%LGP) THEN
  DO JLEV = 1, KFLEVG
    DO JLON = KIDIA, KFDIA
      YDVARS%IRAD%T1(JLON,JLEV) = YDCPG_MISC%QICE(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

IF (YLRAD%LGP) THEN
  DO JLEV = 1, KFLEVG
    DO JLON = KIDIA, KFDIA
      YDVARS%LRAD%T1(JLON,JLEV) = YDCPG_MISC%QLI(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

IF (YA%LGP) THEN
  DO JLEV = 1, KFLEVG
    DO JLON = KIDIA, KFDIA
      YDVARS%A%T1(JLON,JLEV) = YDCPG_MISC%NEB (JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!$ACDC }

! DDH flexibles pour les champs de surface variables/flux/tendances.

IF (YDMODEL%YRML_DIAG%YRLDDH%LFLEXDIA) THEN

!$ACDC ABORT {

  CALL APLPAR_FLEXDIA (YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC, YDMF_PHYS, YDMF_PHYS_SURF, YDMODEL, YDDDH, &
  & YDMF_PHYS_BASE_STATE)

!$ACDC }

ENDIF


IF (LRAFTKE) THEN

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACEVADCAPE {

! DCAPE due to precipitation evaporation.

  CALL ACEVADCAPE(YDMODEL%YRML_PHY_MF%YRPHY2, YDMODEL%YRCST, KIDIA, KFDIA, KLON,                &
  & KFLEVG, YDMF_PHYS%OUT%FPLSL, YDMF_PHYS%OUT%FPLSN, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, &
  & PAPRS, PAPRSF, PT, PCP, PAPHIF, PAPHI, ZDCAPE)

! Gusts.

  CALL ACCLDIA(YDMODEL%YRCST, YDCPG_OPTS%LXCLP, YDCPG_OPTS%LXTGST, YDCPG_OPTS%LXXGST, YDPHY, YDPHY2,                    &
  & YDTOPH, KIDIA, KFDIA, KLON, KFLEVG, YDMF_PHYS%OUT%UCLS,                                                             &
  & YDMF_PHYS%OUT%VCLS, PU, PV, YDMF_PHYS%OUT%CAPE, ZDCAPE, ZTKE1, PAPHIF, YDVARS%GEOMETRY%OROG%T0, YDMF_PHYS%OUT%UGST, &
  & YDMF_PHYS%OUT%VGST, ZBLH, IMOC_CLPH)

  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%CLPH(JLON)=MIN(XMAXLM,MAX(XMINLM,ZBLH(JLON)))
  ENDDO

!$ACDC }

ENDIF
  
!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACVISIH {

ZQGM(1:KLON,1:KFLEVG)=ZEPSNEB

CALL ACVISIH(YDMODEL%YRCST, YDMODEL%YRML_PHY_MF%YRPHY%YRDVISI, KIDIA, KFDIA, KLON,           &
& YDCPG_OPTS%KTDIA, KFLEVG, PAPHI, PAPHIF, PAPRSF, PT, PR, YDCPG_MISC%QLI, YDCPG_MISC%QICE,  &
& ZQR, ZQS, ZQGM, YDMF_PHYS%OUT%VISICLD, YDMF_PHYS%OUT%VISIHYD, YDMF_PHYS%OUT%MXCLWC)

!$ACDC }

! Convert from flux [kg/m2/s] to density [kg/m3] using old RTTOV-SCATT
!  a    b         ! RR = a * LWC^b, [RR]=mm/h, [LWC]=g/m^3
! 20.89 1.15      ! rain
! 29.51 1.10      ! snow

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ZBAY_QRCONV {

DO JLEV=1,KFLEVG
  DO JLON = KIDIA, KFDIA
    ZBAY_QRCONV(JLON,JLEV)  = 0.001_JPRB * ( (ABS(YDMF_PHYS%OUT%FPLCL(JLON,JLEV)) * YDPHY0%RCOEFRAIN(1)) ** YDPHY0%RCOEFRAIN(2) )
    ZBAY_QSCONV(JLON,JLEV)  = 0.001_JPRB * ( (ABS(YDMF_PHYS%OUT%FPLCN(JLON,JLEV)) * YDPHY0%RCOEFSNOW(1)) ** YDPHY0%RCOEFSNOW(2) )    
  ENDDO
ENDDO

!$ACDC }

! Convert density [kg/m3] to mixing ratio [kg/kg]
! R_dry (dry air constant)

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ZDE2MR {

DO JLEV = 1, KFLEVG
  DO JLON = KIDIA, KFDIA
    ZDE2MR(JLON,JLEV)  = YDMODEL%YRCST%RD * PT(JLON,JLEV) / PAPRSF(JLON,JLEV)
    ZBAY_QRCONV(JLON,JLEV) = ZBAY_QRCONV(JLON,JLEV) * ZDE2MR(JLON,JLEV)
    ZBAY_QSCONV(JLON,JLEV) = ZBAY_QSCONV(JLON,JLEV) * ZDE2MR(JLON,JLEV)
  ENDDO
ENDDO

!$ACDC }

! Precipitation Type

! Compute wet-bulb temperature at 2 meters (suppose homogeneity of qv/ql/qi )

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=PPWETPOINT {

CALL PPWETPOINT(YDMODEL%YRCST, YDPHY, KIDIA, KFDIA, KLON, 1, ZPCLS,                          &
& YDMF_PHYS%OUT%TCLS,  YDMF_PHYS_BASE_STATE%Q(:, KFLEVG), YDMF_PHYS_BASE_STATE%L(:, KFLEVG), &
& YDMF_PHYS_BASE_STATE%I(:, KFLEVG),  YDMF_PHYS%OUT%TPWCLS)

!$ACDC }

CALL APL_ARPEGE_DPRECIPS (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, YDVARS, YDMODEL, &
& ZPRC_DPRECIPS, ZPRC_DPRECIPS2)

CALL MF_PHYS_MOCON (YDCPG_BNDS, YDCPG_OPTS, ZRDG_LCVQ, IMOC_CLPH, YDMF_PHYS, YDMF_PHYS_BASE_STATE)

! Store surface water flux P and E for water conservation
IF (YDCPG_OPTS%LCORWAT) THEN

!$ACDC PARALLEL {

  CALL MF_PHYS_CORWAT (YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS%OUT%FEVL, YDMF_PHYS%OUT%FEVN, YDMF_PHYS%OUT%FPLCL,                &
  & YDMF_PHYS%OUT%FPLCN, YDMF_PHYS%OUT%FPLSL, YDMF_PHYS%OUT%FPLSN, YDMF_PHYS_SURF%GSD_VH%PEVA, YDMF_PHYS_SURF%GSD_VH%PPCL, &
  & YDMF_PHYS_SURF%GSD_VH%PPCN, YDMF_PHYS_SURF%GSD_VH%PPSL, YDMF_PHYS_SURF%GSD_VH%PPSN)

!$ACDC }

ENDIF

! Surface specific humidity necessary to compute the vertical advection of q in the case "delta m=1" (unlagged physics only).

IF (NDPSFI == 1) THEN

!$ACDC PARALLEL {

  CALL CPQSOL(YDMODEL%YRCST, YDGEOMETRY%YRDIMV, YDPHY, KLON, KIDIA, KFDIA, &
  & YDCPG_PHY0%PREHYD, YDMF_PHYS_SURF%GSP_RR%PT_T0, YDCPG_MISC%QS, ZFLU_QSATS, YDCPG_MISC%QSOL)

!$ACDC }

ENDIF

CALL APL_ARPEGE_ATMOSPHERE_UPDATE (YDMF_PHYS_BASE_STATE, YDMF_PHYS_NEXT_STATE, YDGEOMETRY,           &
& YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC, YDMF_PHYS, YDCPG_DYN0, YDMF_PHYS_SURF, YDVARS, YDMODEL, YDDDH, &
& ZDIFEXT, ZDSA_CPS, ZMSC_FRMQ, ZPFL_FEFB1, ZPFL_FEFB2, ZPFL_FEFB3, ZPFL_FTKE, ZPFL_FTKEI, ZFCHQ)

CALL MF_PHYS_FPL_PART2 (YDCPG_BNDS, YDCPG_OPTS, ZPFL_FPLCH, ZPFL_FPLSH, YDVARS%CPF%T1, YDVARS%SPF%T1, YDMODEL)

CALL MF_PHYS_TRANSFER (YDCPG_BNDS, YDCPG_OPTS, YDVARS, YDMODEL%YRML_PHY_MF%YRPHY, YDMODEL%YRML_GCONF%YGFL)

CALL APL_ARPEGE_SURFACE_UPDATE (YDCPG_BNDS, YDCPG_OPTS, YDCPG_GPAR, YDMF_PHYS, YDMF_PHYS_SURF, &
& YDMODEL, YDCPG_OPTS%LCONFX, YDCPG_OPTS%ZDTPHY, ZDSA_C1, ZDSA_C2, ZFLU_FEVI, ZFLU_VEG)

IF(YDMODEL%YRML_PHY_MF%YRPHY%LCVPGY) THEN

!$ACDC PARALLEL {

  CALL MF_PHYS_CVV (YDCPG_BNDS, YDCPG_OPTS, YDVARS%CVV%T0, YDVARS%CVV%T1)

!$ACDC }

ENDIF

! Restore the initial value of some pseudo-historical surface buffers if relevant.

IF (YDCPG_OPTS%LCONFX) THEN

  CALL MF_PHYS_SAVE_PHSURF_PART2 (YDCPG_BNDS, YDCPG_OPTS, ZSAV_DDAL, ZSAV_DDOM, ZSAV_ENTCH,            &
  & ZSAV_FHPS, ZSAV_GZ0F, ZSAV_GZ0HF, ZSAV_HV, ZSAV_PBLH, ZSAV_QSH, ZSAV_UDAL, ZSAV_UDGRO, ZSAV_UDOM,  &
  & ZSAV_UNEBH, YDMF_PHYS_SURF%GSD_VF%PZ0F, YDMF_PHYS_SURF%GSD_VH%PPBLH, YDMF_PHYS_SURF%GSD_VH%PQSH,   &
  & YDMF_PHYS_SURF%GSD_VH%PSPSH, YDMF_PHYS_SURF%GSD_VK%PUDGRO, YDMF_PHYS_SURF%GSD_VV%PHV,              &
  & YDMF_PHYS_SURF%GSD_VV%PZ0H, YDVARS%DAL%T0, YDVARS%DOM%T0, YDVARS%UAL%T0, YDVARS%UEN%T0,            &
  & YDVARS%UNEBH%T0, YDVARS%UOM%T0, YDMODEL)

ENDIF

! Store horizontal exchange coefficients (3D turbulence) to SL2 buffers


!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=BAYRAD {

CALL MF_PHYS_BAYRAD (YDCPG_BNDS, YDCPG_OPTS, ZBAY_QRCONV, ZBAY_QSCONV, YDVARS%RCONV%T1, YDVARS%SCONV%T1, YDMODEL)

!$ACDC }


IF (YDEPHY%LEMWAVE) THEN
  CALL APL_ARPEGE_MWAVE (YDCPG_MISC, YDMF_PHYS, YDCPG_BNDS, YDCPG_OPTS, YDPHYSMWAVE)
ENDIF


!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=EDR {

IF (LEDR) THEN
  DO JLEV = 1, YDCPG_OPTS%YRSURF_DIMS%YSD_DID%NLEVS
    YDMF_PHYS_SURF%GSD_DI%PXEDR(:,JLEV)=YDMF_PHYS%OUT%EDR(:,JLEV)
  ENDDO
ENDIF

!$ACDC }

!$ACDC PARALLEL,TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=PRECIPS {

CALL MF_PHYS_PRECIPS (YDCPG_BNDS, YDCPG_OPTS, ZPRC_DPRECIPS, ZPRC_DPRECIPS2, YDMF_PHYS_SURF%GSD_XP%PPRECIP, &
& YDMF_PHYS_SURF%GSD_XP2%PPRECIP2, YDMODEL)

!$ACDC }


END ASSOCIATE
END ASSOCIATE
END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('APL_ARPEGE',1,ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE

