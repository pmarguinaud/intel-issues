MODULE YOE_PHYS_MWAVE

USE PARKIND1  ,ONLY : JPRB, JPIM
USE FIELD_MODULE
USE FIELD_FACTORY_MODULE

IMPLICIT NONE


PRIVATE 

!       ----------------------------------------------------------------
!PHYSICS VARIABLES FOR MWAVE OPERATOR
!     D.Salmond      E.C.M.W.F.    23 Jan 2018
!     Moved out these arrays from GFL     
!       ----------------------------------------------------------------

TYPE,PUBLIC :: TEPHYSMWAVE
  LOGICAL :: LPHYS_MWAVE_FILLED_IN=.FALSE.
  REAL(KIND=JPRB),POINTER :: PHYS_MWAVE(:,:,:,:) => NULL ()
  REAL(KIND=JPRB),POINTER :: P(:,:,:) => NULL ()
  CLASS (FIELD_4RB), POINTER :: F_PHYS_MWAVE => NULL ()

  CONTAINS
  PROCEDURE :: CREATE
  PROCEDURE :: DESTROY
  PROCEDURE :: ZERO
  PROCEDURE :: COPY
  PROCEDURE :: UPDATE_VIEW
END TYPE TEPHYSMWAVE

INTEGER(KIND=JPIM),PUBLIC :: N_PHYS_MWAVE

CONTAINS
!============================================================================
SUBROUTINE CREATE(SELF,YDGEOMETRY)
USE GEOMETRY_MOD , ONLY : GEOMETRY
IMPLICIT NONE
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
CLASS(TEPHYSMWAVE) :: SELF

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV)
ASSOCIATE(NPROMA=>YDDIM%NPROMA, NFLEVG=>YDDIMV%NFLEVG, &
 & NGPBLKS=>YDDIM%NGPBLKS)
N_PHYS_MWAVE=9 !THIS IS POOR CODING - NEEDS TO BE CLEANED UP.
               !THIS NUMBER MUST BE THE SAME AS IN ifs/module/par_gfl.F90
ALLOCATE(SELF%PHYS_MWAVE(NPROMA,NFLEVG,N_PHYS_MWAVE,NGPBLKS))

SELF%PHYS_MWAVE(:,:,:,:) = 0.0_JPRB

CALL FIELD_NEW (SELF%F_PHYS_MWAVE, DATA=SELF%PHYS_MWAVE, PERSISTENT=.TRUE.)

END ASSOCIATE
END ASSOCIATE

END SUBROUTINE CREATE
!============================================================================
SUBROUTINE UPDATE_VIEW (SELF, BLOCK_INDEX)

CLASS (TEPHYSMWAVE) :: SELF
INTEGER (KIND=JPIM), INTENT (IN) :: BLOCK_INDEX

IF (ASSOCIATED (SELF%F_PHYS_MWAVE)) THEN
  SELF%P => SELF%F_PHYS_MWAVE%GET_VIEW (BLOCK_INDEX)
ENDIF

END SUBROUTINE UPDATE_VIEW
!============================================================================
SUBROUTINE DESTROY(SELF)
IMPLICIT NONE
CLASS(TEPHYSMWAVE) :: SELF

IF (ASSOCIATED (SELF%F_PHYS_MWAVE)) CALL FIELD_DELETE (SELF%F_PHYS_MWAVE)
SELF%F_PHYS_MWAVE => NULL ()
SELF%P => NULL ()
DEALLOCATE(SELF%PHYS_MWAVE)

END SUBROUTINE DESTROY
!============================================================================

SUBROUTINE ZERO(SELF)
CLASS(TEPHYSMWAVE) :: SELF
IF(ASSOCIATED(SELF%PHYS_MWAVE)) SELF%PHYS_MWAVE=0.0_JPRB
END SUBROUTINE ZERO
!============================================================================
SUBROUTINE COPY(SELF,RHS)
CLASS(TEPHYSMWAVE) :: SELF
CLASS(TEPHYSMWAVE) :: RHS
!---------------------------------------------------------------------------
IF(ASSOCIATED(SELF%PHYS_MWAVE).AND.ASSOCIATED(RHS%PHYS_MWAVE)) THEN
  IF (ALL(SHAPE(SELF%PHYS_MWAVE) == SHAPE(RHS%PHYS_MWAVE))) THEN
    SELF%PHYS_MWAVE=RHS%PHYS_MWAVE
  ELSE
    CALL ABOR1("YOE_PHYS_MWAVE:COPY - DIFFERNT SHAPES")
  ENDIF
ELSE
  CALL ABOR1("YOE_PHYS_MWAVE:COPY - UNALLOCATED ARRAY")
ENDIF
!---------------------------------------------------------------------------
END SUBROUTINE COPY

END MODULE YOE_PHYS_MWAVE
