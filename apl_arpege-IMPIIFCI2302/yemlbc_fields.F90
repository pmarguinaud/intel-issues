MODULE YEMLBC_FIELDS 

! Purpose :
! -------
!    Forcing a LAM model by another model: part 0C
!    - forcing by lateral boundary conditions
!    - pressure tendency coupling
!    - spectral nudging

! Interface :
! ---------
!    Empty.

! External :
! --------
!    None.

! Method :
! ------
!    See Documentation.

! Reference :
! ---------

! Author :
! ------
!    Bogdan Bochenek - part from ELBC0B
! Original : Sep 2014

! Modifications :
! -------------
! H. Dhouioui (Sep 2017) Renamed from elbc0c_mod.F90
!-----------------------------------------------------------------------------

USE PARKIND1  , ONLY : JPIM, JPRB
USE YOMHOOK   , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMCT0    , ONLY : LALLOPR, NCONF
USE YOMLUN    , ONLY : NULOUT
USE YOMGMV    , ONLY : TGMV
USE YEMLBC_MODEL , ONLY : TELBC_MODEL
IMPLICIT NONE
SAVE

!=============================================================================

!      1.    DECLARATIONS
!            ------------


TYPE :: TELBC_FIELDS 


!      1.2   Other variables for grid-point coupling.

! GMVCPL, GMVSCPL, GFLCPL: buffers containing the LBC for GMV, GMVS, GFL
! GMVUCPL, GMVSUCPL, GFLUCPL: buffers containing the UBC for GMV, GMVS, GFL
! GMVSTENC       : cf. GMVSCPL but for LTENC.
! MGMV0          : index for "GMV" memory transfers between GMV and coupling buffers.
! MGMV1          : index for "GMV" memory transfers between GMVT1 and coupling buffers.
! MGMVS0         : index for "GMVS" memory transfers between GMVS and coupling buffers.
! MGMVS1         : index for "GMVS" memory transfers between GMVT1S and coupling buffers.
! CCFIELD_GMV, CCFIELD_GMVS, CCFIELD_GFL: fields names for EWRLSGRAD.

REAL(KIND=JPRB),ALLOCATABLE:: GMVCPL(:,:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVSCPL(:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GFLCPL(:,:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVUCPL(:,:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVSUCPL(:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GFLUCPL(:,:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVSTENC(:,:,:,:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMV0(:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMV1(:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMVS0(:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMVS1(:)
CHARACTER(LEN=12),ALLOCATABLE:: CCFIELD_GMV(:)
CHARACTER(LEN=12),ALLOCATABLE:: CCFIELD_GMVS(:)
CHARACTER(LEN=12),ALLOCATABLE:: CCFIELD_GFL(:)

!      1.3   Other variables for spectral nudging.

! GT3SPBUF   : buffer for spectral boundary fields
! NOFFGT3BSP : Supposed to be half the size of GT3SPBUF (which contains 2 sets of fields)

REAL(KIND=JPRB),ALLOCATABLE :: GT3SPBUF(:)
INTEGER(KIND=JPIM) :: NOFFGT3BSP

END TYPE TELBC_FIELDS 


!=============================================================================

CONTAINS

!      2.    SET-UP

SUBROUTINE SUELBC_FIELDS(YDELBC_FIELDS,YDML_LBC,YDGEOMETRY,YDDYNA,YDGMV,YGFL,KNFD2D,KNS3D)

!--------------------------------------------------------------------------
! Sets-up part 0C of forcing a LAM model by another model
!--------------------------------------------------------------------------

!--------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOM_YGFL     , ONLY : TYPE_GFLD
USE YOMDYNA      , ONLY : TDYNA
TYPE(TELBC_FIELDS)     , INTENT(INOUT), POINTER :: YDELBC_FIELDS 
TYPE(TELBC_MODEL)     , INTENT(INOUT) :: YDML_LBC
TYPE(GEOMETRY)    , INTENT(IN)    :: YDGEOMETRY
TYPE(TDYNA)       , INTENT(IN)    :: YDDYNA
TYPE(TGMV)        , INTENT(INOUT) :: YDGMV
TYPE(TYPE_GFLD)   , INTENT(INOUT) :: YGFL
INTEGER(KIND=JPIM), INTENT(IN)    :: KNFD2D
INTEGER(KIND=JPIM), INTENT(IN)    :: KNS3D


REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
INTEGER(KIND=JPIM) :: IWS_TENC, ICPL, JGFL, IW, IW_TENC

!--------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:SUELBC_FIELDS',0,ZHOOK_HANDLE)
!--------------------------------------------------------------------------

ASSOCIATE(LCCPL=>YDML_LBC%LCCPL,LQCPL=>YDML_LBC%LQCPL,LTENC=>YDML_LBC%LTENC, &
 & LALLTC=>YDML_LBC%LALLTC,LESPCPL=>YDML_LBC%LESPCPL,LSPTENC=>YDML_LBC%LSPTENC, &
 & LUNBC=>YDML_LBC%LUNBC)
ALLOCATE(YDELBC_FIELDS)


! * Allocation of GMVCPL, GMVSCPL, GFLCPL, GMVSTENC.

IF (LCCPL) THEN
  IW=4
  IW_TENC=4
ELSEIF (LQCPL) THEN
  IW=3
  IW_TENC=3
ELSEIF (LTENC.AND.LALLTC) THEN
  IW=3
  IW_TENC=3
ELSE
  IW=2
  IW_TENC=2
ENDIF

ALLOCATE (YDELBC_FIELDS%GMVCPL(YDGEOMETRY%YRDIM%NPROMA,IW,YDGEOMETRY%YRDIMV%NFLEVG,YDML_LBC%YYTGMVCPL%NDIMT,YDGEOMETRY%YRELBC_GEO%NCPLBLKS))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%GMVCPL   ',SIZE(YDELBC_FIELDS%GMVCPL   ),SHAPE(YDELBC_FIELDS%GMVCPL   )
ALLOCATE (YDELBC_FIELDS%GMVSCPL(YDGEOMETRY%YRDIM%NPROMA,IW,YDML_LBC%YYTGMVSCPL%NDIMT,YDGEOMETRY%YRELBC_GEO%NCPLBLKS))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%GMVSCPL  ',SIZE(YDELBC_FIELDS%GMVSCPL  ),SHAPE(YDELBC_FIELDS%GMVSCPL  )
ALLOCATE (YDELBC_FIELDS%GFLCPL(YDGEOMETRY%YRDIM%NPROMA,IW,YDGEOMETRY%YRDIMV%NFLEVG,YDML_LBC%NDIMCPL,YDGEOMETRY%YRELBC_GEO%NCPLBLKS))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%GFLCPL   ',SIZE(YDELBC_FIELDS%GFLCPL   ),SHAPE(YDELBC_FIELDS%GFLCPL   )
IF(LUNBC) THEN
  ALLOCATE (YDELBC_FIELDS%GMVUCPL(YDGEOMETRY%YRDIM%NPROMA,IW,YDGEOMETRY%YRDIMV%NFLEVG,YDML_LBC%YYTGMVCPL%NDIMT,YDGEOMETRY%YRDIM%NGPBLKS))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
   & 'YDELBC_FIELDS%GMVUCPL   ',SIZE(YDELBC_FIELDS%GMVUCPL   ),SHAPE(YDELBC_FIELDS%GMVUCPL   )
  ALLOCATE (YDELBC_FIELDS%GMVSUCPL(YDGEOMETRY%YRDIM%NPROMA,IW,YDML_LBC%YYTGMVSCPL%NDIMT,YDGEOMETRY%YRDIM%NGPBLKS))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
   & 'YDELBC_FIELDS%GMVSUCPL  ',SIZE(YDELBC_FIELDS%GMVSUCPL  ),SHAPE(YDELBC_FIELDS%GMVSUCPL  )
  ALLOCATE (YDELBC_FIELDS%GFLUCPL(YDGEOMETRY%YRDIM%NPROMA,IW,YDGEOMETRY%YRDIMV%NFLEVG,YDML_LBC%NDIMCPL,YDGEOMETRY%YRDIM%NGPBLKS))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
   & 'YDELBC_FIELDS%GFULCPL   ',SIZE(YDELBC_FIELDS%GFLUCPL   ),SHAPE(YDELBC_FIELDS%GFLUCPL   )
ENDIF

ALLOCATE (YDELBC_FIELDS%GMVSTENC(YDGEOMETRY%YRDIM%NPROMA,IW_TENC,YDML_LBC%YYTGMVSTENC%NDIMT,YDGEOMETRY%YRELBC_GEO%NCPLBLKS))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
& 'YDELBC_FIELDS%GMVSTENC ',SIZE(YDELBC_FIELDS%GMVSTENC ),SHAPE(YDELBC_FIELDS%GMVSTENC )

! * Allocation and computation of MGMV0, MGMV1, MGMVS0, MGMVS1:

ALLOCATE (YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%NDIMT))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%MGMV0    ',SIZE(YDELBC_FIELDS%MGMV0    ),SHAPE(YDELBC_FIELDS%MGMV0    )
YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MU)=YDGMV%YT0%MU
YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MV)=YDGMV%YT0%MV
YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MT)=YDGMV%YT0%MT
IF (YDDYNA%LNHDYN) YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MSPD)=YDGMV%YT0%MSPD
IF (YDDYNA%LNHDYN) YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MSVD)=YDGMV%YT0%MSVD
IF (YDDYNA%LNHDYN.AND.YDDYNA%LNHX) YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MNHX)=YDGMV%YT0%MNHX
IF (NCONF /= 701) THEN
  YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MDIV)=YDGMV%YT0%MDIV
  YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MTL)=YDGMV%YT0%MTL
  YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MTM)=YDGMV%YT0%MTM
  IF (YDDYNA%LNHDYN) YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MSPDL)=YDGMV%YT0%MSPDL
  IF (YDDYNA%LNHDYN) YDELBC_FIELDS%MGMV0(YDML_LBC%YYTGMVCPL%MSPDM)=YDGMV%YT0%MSPDM
ENDIF

ALLOCATE (YDELBC_FIELDS%MGMV1(YDML_LBC%YYTGMVCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
& 'YDELBC_FIELDS%MGMV1    ',SIZE(YDELBC_FIELDS%MGMV1    ),SHAPE(YDELBC_FIELDS%MGMV1    )
YDELBC_FIELDS%MGMV1(YDML_LBC%YYTGMVCPL%MU)=YDGMV%YT1%MU
YDELBC_FIELDS%MGMV1(YDML_LBC%YYTGMVCPL%MV)=YDGMV%YT1%MV
YDELBC_FIELDS%MGMV1(YDML_LBC%YYTGMVCPL%MT)=YDGMV%YT1%MT
IF (YDDYNA%LNHDYN) YDELBC_FIELDS%MGMV1(YDML_LBC%YYTGMVCPL%MSPD)=YDGMV%YT1%MSPD
IF (YDDYNA%LNHDYN) YDELBC_FIELDS%MGMV1(YDML_LBC%YYTGMVCPL%MSVD)=YDGMV%YT1%MSVD
IF (YDDYNA%LNHDYN.AND.YDDYNA%LNHX) YDELBC_FIELDS%MGMV1(YDML_LBC%YYTGMVCPL%MNHX)=YDGMV%YT1%MNHX

ALLOCATE (YDELBC_FIELDS%MGMVS0(YDML_LBC%YYTGMVSCPL%NDIMT))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%MGMVS0   ',SIZE(YDELBC_FIELDS%MGMVS0   ),SHAPE(YDELBC_FIELDS%MGMVS0   )
YDELBC_FIELDS%MGMVS0(YDML_LBC%YYTGMVSCPL%MSP)=YDGMV%YT0%MSP
IF (NCONF /= 701) THEN
  YDELBC_FIELDS%MGMVS0(YDML_LBC%YYTGMVSCPL%MSPL)=YDGMV%YT0%MSPL
  YDELBC_FIELDS%MGMVS0(YDML_LBC%YYTGMVSCPL%MSPM)=YDGMV%YT0%MSPM
ENDIF

ALLOCATE (YDELBC_FIELDS%MGMVS1(YDML_LBC%YYTGMVSCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%MGMVS1   ',SIZE(YDELBC_FIELDS%MGMVS1   ),SHAPE(YDELBC_FIELDS%MGMVS1   )
YDELBC_FIELDS%MGMVS1(YDML_LBC%YYTGMVSCPL%MSP)=YDGMV%YT1%MSP

! * Allocation and computation of CCFIELD_GMV, CCFIELD_GMVS, CCFIELD_GFL:

ALLOCATE (YDELBC_FIELDS%CCFIELD_GMV(YDML_LBC%YYTGMVCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A12,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%CCFIELD_GMV ',SIZE(YDELBC_FIELDS%CCFIELD_GMV),SHAPE(YDELBC_FIELDS%CCFIELD_GMV)
YDELBC_FIELDS%CCFIELD_GMV(YDML_LBC%YYTGMVCPL%MU)='LSCGRAD_UUUU'
YDELBC_FIELDS%CCFIELD_GMV(YDML_LBC%YYTGMVCPL%MV)='LSCGRAD_VVVV'
YDELBC_FIELDS%CCFIELD_GMV(YDML_LBC%YYTGMVCPL%MT)='LSCGRAD_TEMP'
IF (YDDYNA%LNHDYN) YDELBC_FIELDS%CCFIELD_GMV(YDML_LBC%YYTGMVCPL%MSPD)='LSCGRAD_PDEP'
IF (YDDYNA%LNHDYN) YDELBC_FIELDS%CCFIELD_GMV(YDML_LBC%YYTGMVCPL%MSVD)='LSCGRAD_VDIV'
IF (YDDYNA%LNHDYN.AND.YDDYNA%LNHX) YDELBC_FIELDS%CCFIELD_GMV(YDML_LBC%YYTGMVCPL%MNHX)='LSCGRAD_NHXX'

ALLOCATE (YDELBC_FIELDS%CCFIELD_GMVS(YDML_LBC%YYTGMVSCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A12,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%CCFIELD_GMVS',SIZE(YDELBC_FIELDS%CCFIELD_GMVS),SHAPE(YDELBC_FIELDS%CCFIELD_GMVS)
YDELBC_FIELDS%CCFIELD_GMVS(YDML_LBC%YYTGMVSCPL%MSP)='LSCGRAD_SURP'

ALLOCATE (YDELBC_FIELDS%CCFIELD_GFL(YDML_LBC%NDIMCPL))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A12,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%CCFIELD_GFL ',SIZE(YDELBC_FIELDS%CCFIELD_GFL),SHAPE(YDELBC_FIELDS%CCFIELD_GFL)
YDELBC_FIELDS%CCFIELD_GFL(1:YDML_LBC%NDIMCPL)='LSCGRAD_HUMQ'

! * Other variables for spectral nudging: NOFFGT3BSP, allocate GT3SPBUF

IWS_TENC=2

IF (LESPCPL) THEN
  YDELBC_FIELDS%NOFFGT3BSP=YDGEOMETRY%YRDIM%NSPEC2*(KNS3D*YDGEOMETRY%YRDIMV%NFLEVL+KNFD2D)
ELSE
  YDELBC_FIELDS%NOFFGT3BSP=0
ENDIF

IF (LESPCPL) THEN
  IF (LSPTENC) IWS_TENC=3
  ALLOCATE(YDELBC_FIELDS%GT3SPBUF(IWS_TENC*YDELBC_FIELDS%NOFFGT3BSP))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)") 'YDELBC_FIELDS%GT3SPBUF'&
& ,SIZE(YDELBC_FIELDS%GT3SPBUF ),SHAPE(YDELBC_FIELDS%GT3SPBUF )
ENDIF

!--------------------------------------------------------------------------

!      Part B: printings.

WRITE(NULOUT,*) ' --- PRINTINGS IN SUELBC_FIELDS --- '


! * B2: Other variables for grid-point coupling.
WRITE(UNIT=NULOUT,FMT='('' Other variables for grid-point coupling: '')')
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMV0 = '',20(1X,I2))') YDELBC_FIELDS%MGMV0(1:YDML_LBC%YYTGMVCPL%NDIMT)
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMVS0 = '',20(1X,I2))') YDELBC_FIELDS%MGMVS0(1:YDML_LBC%YYTGMVSCPL%NDIMT)
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMV1 = '',20(1X,I2))') YDELBC_FIELDS%MGMV1(1:YDML_LBC%YYTGMVCPL%NDIM)
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMVS1 = '',20(1X,I2))') YDELBC_FIELDS%MGMVS1(1:YDML_LBC%YYTGMVSCPL%NDIM)

! * D3: Other variables for spectral nudging.
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%NOFFGT3BSP = '',I8)') YDELBC_FIELDS%NOFFGT3BSP

WRITE(NULOUT,*) ' '
END ASSOCIATE
!--------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:SUELBC_FIELDS',1,ZHOOK_HANDLE)
END SUBROUTINE SUELBC_FIELDS 

SUBROUTINE DEALLOCATE_ELBC0C(YDELBC_FIELDS)

!--------------------------------------------------------------------------
! deallocates 'ELBC0C' arrays
!--------------------------------------------------------------------------

IMPLICIT NONE

TYPE(TELBC_FIELDS) , INTENT(INOUT) :: YDELBC_FIELDS
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:DEALLOCATE_ELBC0C',0,ZHOOK_HANDLE)
!     ------------------------------------------------------------------

IF (ALLOCATED(YDELBC_FIELDS%GMVCPL  )) DEALLOCATE(YDELBC_FIELDS%GMVCPL)
IF (ALLOCATED(YDELBC_FIELDS%GMVSCPL )) DEALLOCATE(YDELBC_FIELDS%GMVSCPL)
IF (ALLOCATED(YDELBC_FIELDS%GFLCPL  )) DEALLOCATE(YDELBC_FIELDS%GFLCPL)
IF (ALLOCATED(YDELBC_FIELDS%GMVSTENC)) DEALLOCATE(YDELBC_FIELDS%GMVSTENC)
IF (ALLOCATED(YDELBC_FIELDS%GT3SPBUF)) DEALLOCATE(YDELBC_FIELDS%GT3SPBUF)

IF (ALLOCATED(YDELBC_FIELDS%GMVUCPL )) DEALLOCATE(YDELBC_FIELDS%GMVUCPL)
IF (ALLOCATED(YDELBC_FIELDS%GMVSUCPL)) DEALLOCATE(YDELBC_FIELDS%GMVSUCPL)
IF (ALLOCATED(YDELBC_FIELDS%GFLUCPL )) DEALLOCATE(YDELBC_FIELDS%GFLUCPL)

IF (ALLOCATED(YDELBC_FIELDS%MGMV0  )) DEALLOCATE(YDELBC_FIELDS%MGMV0)
IF (ALLOCATED(YDELBC_FIELDS%MGMV1  )) DEALLOCATE(YDELBC_FIELDS%MGMV1)
IF (ALLOCATED(YDELBC_FIELDS%MGMVS0  )) DEALLOCATE(YDELBC_FIELDS%MGMVS0)
IF (ALLOCATED(YDELBC_FIELDS%MGMVS1  )) DEALLOCATE(YDELBC_FIELDS%MGMVS1)

IF (ALLOCATED(YDELBC_FIELDS%CCFIELD_GMV)) DEALLOCATE(YDELBC_FIELDS%CCFIELD_GMV)
IF (ALLOCATED(YDELBC_FIELDS%CCFIELD_GMVS)) DEALLOCATE(YDELBC_FIELDS%CCFIELD_GMVS)
IF (ALLOCATED(YDELBC_FIELDS%CCFIELD_GFL)) DEALLOCATE(YDELBC_FIELDS%CCFIELD_GFL)

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:DEALLOCATE_ELBC0C',1,ZHOOK_HANDLE)
END SUBROUTINE DEALLOCATE_ELBC0C

!=============================================================================

END MODULE YEMLBC_FIELDS 
